import * from "std/fs"
import * from "std/text"

/// Get the variable from the environment, otherwise check from the input file
pub fun get_env_var(var: Text, file: Text = ".env"): Text {
    let _var = unsafe $echo "\$\{!var}"$
    if _var != "" {
        return _var
    }

    if file_exist(".env") {
        unsafe $source "{file}"$
        return unsafe $echo "\$\{!var}"$
    }

    return ""
}

/// Load the env file in the environment
pub fun load_env_file(file: Text = ".env"): Null {
    unsafe $export "\$(xargs < {file})" > /dev/null$
}

/// ?
pub fun shell_isset(name: Text): Bool {
    $[[ ! -z \$\{!{nameof name}+z} ]]$ failed {
        return false
    }
    return true
}

/// Set a constant
pub fun shell_constant_set(name: Text, val: Text): Null {
    $readonly \${nameof name}="\${nameof val}" 2> /dev/null$?
}

/// Get a constant
pub fun shell_constant_get(name: Text): Text {
    return $echo \$\{!{nameof name}}$?
}

/// Set a variable?
pub fun shell_var_set(name: Text, val: Text): Null {
    $export \${nameof name}="\${nameof val}" 2> /dev/null$?
}

/// Get a variable?
pub fun shell_var_get(name: Text): Text {
    return $echo \$\{!{nameof name}}$?
}

/// Remove a variable?
pub fun shell_unset(name: Text): Null {
    $unset {name}$?
}

/// Check if the command exist
pub fun is_command(command: Text): Bool {
    $[ -x "\$(command -v {command})" ]$ failed {
        return false
    }
    return true
}

/// Create a prompt and return the value
pub fun input(prompt: Text): Text {
    unsafe $printf "\${nameof prompt}"$
    unsafe $read$
    return "\$REPLY"
}

/// Check if the command passed fails
pub fun has_failed(command: Text): Bool {
    unsafe silent $eval {command}$
    return status != 0
}

/// Close the script
pub fun exit(code: Num): Null {
    unsafe $exit "{code}"$
}

/// Check if the script is running with a user with root permission
pub fun is_root(): Bool {
    if unsafe $id -u$ == "0" {
        return true
    }

    return false
}

/// ?
pub fun printf(format: Text, args: [Text] = [""]): Null {
    unsafe ${nameof args}=("{format}" "\$\{{nameof args}[@]}")$
    unsafe $printf "\$\{{nameof args}[@]}"$
}

/// ?
pub fun printf_escape(text: Text): Text {
    return unsafe $echo \${nameof text} | sed -e 's/\\\\/\\\\\\\\/g' -e "s/%/%%/g"$
}

/// Print a string with formatting options
pub fun text_shell(message: Text, style: Num, fg: Num, bg: Num): Text {
    return "\x1b[{style};{fg};{bg}m{printf_escape(message)}\x1b[0m"
}

/// Print a string as bold
pub fun text_bold(message: Text): Text {
    return "\x1b[1m{printf_escape(message)}\x1b[0m"
}

/// Print a string as italic
pub fun text_italic(message: Text): Text {
    return "\x1b[3m{printf_escape(message)}\x1b[0m"
}

/// Print a string as underlined
pub fun text_underlined(message: Text): Text {
    return "\x1b[4m{printf_escape(message)}\x1b[0m"
}

/// Print a string with a specified color
pub fun color_echo(message: Text, color: Num): Null {
    printf("\x1b[{color as Text}m%s\x1b[0m\n", [message])
}

/// Print a string as Info
pub fun echo_info(message: Text): Null {
    printf("\x1b[1;3;97;44m %s \x1b[0m\n", [message])
}

/// Print a string as Success
pub fun echo_success(message: Text): Null {
    printf("\x1b[1;3;97;42m %s \x1b[0m\n", [message])
}

/// Print a string as Warning
pub fun echo_warning(message: Text): Null {
    printf("\x1b[1;3;97;43m %s \x1b[0m\n", [message])
}

/// Print a string as Error
pub fun error(message: Text, exit_code: Num = 1): Null {
    printf("\x1b[1;3;97;41m %s \x1b[0m\n", [message])
    if exit_code > 0 : exit(exit_code)
}
